import win.ui;
/*DSG{{*/
var winform = win.form(text="发布";right=191;bottom=191;border="none";max=false;min=false;mode="popup")
winform.add(
btnCopy={cls="button";text="复制内网地址";left=16;top=88;right=176;bottom=120;dl=1;dt=1;font=LOGFONT(h=-16);z=3};
btnOpenDir={cls="button";text="打开文件夹";left=16;top=40;right=176;bottom=72;dl=1;dt=1;font=LOGFONT(h=-16);z=2};
btnPublish={cls="button";text="发布";left=16;top=136;right=88;bottom=168;dl=1;dt=1;font=LOGFONT(h=-16);z=1};
btnVisit={cls="button";text="访问";left=104;top=136;right=176;bottom=168;dl=1;dt=1;font=LOGFONT(h=-16);z=4}
)
/*}}*/

import win.ui.simpleWindow;
win.ui.simpleWindow(winform,,,,).skin(
	background = { 
		hover = 0xff99ffcc;
		active = 0xffff6666; 
		default = 0x00000000; 
	}
	color = { 
		hover = 0xff666666;
		active = 0xffffffff; 
		default = 0xFF7BA5BF; 
	}
)

//下面创建一个手动跟踪模式的提示控件
import win.ui.tooltip;
var balloonFlagShow = win.ui.tooltip.tracking(winform,false);

winform.htmlPath = winform.parent.pathGen();
winform.parent.modalMask.show(true);

winform.btnPublish.oncommand = function(id,event){
	var flag = winform.parent.saveToPublish();
	if(flag){
		var x,y,cx,cy = winform.btnPublish.getPos(true);
    	balloonFlagShow.setText("发布成功！右侧*访问*打开浏览器！").trackPopup(true,x+cx*3/4,y+cy );
    	win.delay(1600);
    	balloonFlagShow.trackPopup(false);
	}
}

winform.btnVisit.oncommand = function(id,event){
	..process.execute(winform.LANaddress);
}

winform.btnCopy.oncommand = function(id,event){
	var flag = ..win.clip.write(winform.LANaddress,false);
	if(flag){
		var x,y,cx,cy = winform.btnCopy.getPos(true);
    	balloonFlagShow.setText("复制成功！Ctrl+C可粘贴！").trackPopup(true,x+cx*3/4,y+cy );
    	win.delay(1600);
    	balloonFlagShow.trackPopup(false);
	}
}

winform.btnOpenDir.oncommand = function(id,event){
	..process("explorer.exe",io.fullpath(..siteBaseDir))
}

winform.onClose = function(hwnd,message,wParam,lParam){
    winform.parent.modalMask.show(false);
}

winform.uiWait = function(trueOrFalse){
	if(trueOrFalse){
		winform.btnVisit.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷"};
		winform.btnCopy.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷"};
	}
	else {
		winform.btnVisit.disabledText = null;
		winform.btnCopy.disabledText = null;
	}
	
}

winform.LANaddressReady = function(){
	winform.LANaddress = ..serverMain.getUrl(winform.htmlPath,true);
	winform.uiWait(false);
}

winform.uiWait(true);
winform.show();
winform.LANaddressReady();
win.loopMessage();
return winform;