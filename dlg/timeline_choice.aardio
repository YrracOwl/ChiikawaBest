import fonts.fontAwesome;

import win.ui;
import win.ui.atom;
/*DSG{{*/
var winform = win.form(text="选择时间线";right=295;bottom=175;border="none";max=false)
winform.add(
["btn创建时间线"]={cls="plus";text="创建时间线(Ctrl+G)";left=72;top=144;right=232;bottom=174;align="left";bgcolor=-5197169;border={radius=16};dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome');padding={left=10;bottom=-2}};iconText='\uF02D';notify=1;textPadding={left=30};z=4};
["btn删除时间线"]={cls="plus";text="删除";left=32;top=64;right=108;bottom=94;align="left";bgcolor=1587666;border={radius=16};dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome');padding={left=10}};iconText='\uF014';notify=1;textPadding={left=28};z=5};
["btn打开时间线"]={cls="plus";text="打开(Ctrl+D)";left=144;top=64;right=264;bottom=94;align="left";bgcolor=-5197169;border={radius=16};dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome');padding={left=10;bottom=-2}};iconText='\uF07C';notify=1;textPadding={left=30;top=1};z=3};
["txt时间线标题"]={cls="plus";left=16;top=96;right=280;bottom=136;border={bottom=1;color=-6908266};dl=1;dt=1;editable="edit";font=LOGFONT(h=-16);notify=1;tabstop=1;textPadding={top=16};z=1};
选择时间线={cls="combobox";left=16;top=32;right=280;bottom=56;border=1;clipch=1;dl=1;dt=1;font=LOGFONT(h=-13);items={};mode="dropdown";tabstop=1;z=2}
)
/*}}*/

var atom,hwnd/*冲突窗口的句柄,该函数会自动激活此窗口*/ = winform.atom("2B31E250-5A0B-478C-B373-DE23820BEA2F.timelineChoice");
if(!atom){
	/*为窗口设置原子值可以避免一个程序重复运行多个实例*/
	win.setForeground(hwnd)
	return;
}

import fsys;

import win.ui.tooltip; 
//下面创建一个手动跟踪模式的提示控件
var balloonTipCtrl = win.ui.tooltip.tracking(winform,false);
winform.balloonTipCtrlState = false;

winform.txt时间线标题.text = "";
winform.txt时间线标题.setCueBannerText("请输入时间线标题");

winform.btn删除时间线.skin({
	background={
		default=0xFFE32636;
		disabled=0xFFCCCCCC;
		hover=0xFFFFF5EE
	};
	color={
		default=0xFF000000;
		disabled=0xFF6D6D6D
	}
})

winform.btn创建时间线.skin({
	background={
		default=0x668FB2B0;
		disabled=0xFFCCCCCC;
		hover=0xFF928BB3
	};
	color={
		default=0xFF000000;
		disabled=0xFF6D6D6D
	}
})

winform.btn打开时间线.skin({
	background={
		default=0x668FB2B0;
		disabled=0xFFCCCCCC;
		hover=0xFF928BB3
	};
	color={
		default=0xFF000000;
		disabled=0xFF6D6D6D
	}
})

winform.txt时间线标题.skin(
	background = { 
		hover = "/res/images/btn-hover.png";
		focus = "/res/images/btn-focus.jpg";
		active = "/res/images/btn-active.png";
		disabled = "/res/images/btn-disabled.png"; 
	}
)

import win.ui.mask;
var frmMask = win.ui.mask(winform,true)

var updateCombobox = function(winform){
	var list_timeline = {};
	..logMsg(">>>更新items")
	for rowid, name, time in ..dbBase.each("SELECT rowid,* FROM timeline") {
		/*
		for 语句的第一个返回值如果为 null 就会退出循环,
		如果数据库返回的第一个字段可能为空,那么可以把 SQLite 默认就有的索引字段 rowid 放在最前面
		*/
		table.push(list_timeline,name)
		..logMsg(rowid,time,name)
	}
	winform.选择时间线.items = list_timeline;
	winform.选择时间线.selIndex = table.len(list_timeline);
};

winform.btn打开时间线.oncommand = function(id,event){
	
	winform.btn打开时间线.disabled = true;
	
	//winform.show(false)
	frmMask.show(true);
	winform.btn打开时间线.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250';text='打开中...'}
	
	var timelineChild = win.loadForm("\dlg\timeline.aardio",,win.getDesktop());
	
	timelineChild.childWinUpdate(winform.选择时间线.selText);
	timelineChild.listboxUpdate(timelineChild);
	timelineChild.timelineWebUpdate(timelineChild);
	
	winform.btn打开时间线.disabledText = null;
	frmMask.show(false);
	//winform.close();
	
	win.delay(1000);
	winform.btn打开时间线.disabled = false;
	
	winform.close();
}

updateCombobox(winform);

winform.btn创建时间线.oncommand = function(id,event){
	
	winform.btn创建时间线.disabled = true;
	
	if(winform.txt时间线标题.text != ""){
		try{
			if( not ..dbTimeline.existsTable(winform.txt时间线标题.text) ){ 
				..dbTimeline.exec( "CREATE TABLE ?? (time PRIMARY KEY, title, content);",{winform.txt时间线标题.text}); 
				..logWrite(">>>Timeline数据库新增表：",winform.txt时间线标题.text)
			}
			var cmd = ..dbBase.prepare("REPLACE INTO [timeline] VALUES ( @name,@time);");
			cmd.step(
				name = winform.txt时间线标题.text;
				time = time.now();
			)
			..logWrite(">>>Base数据库新增Timeline：",winform.txt时间线标题.text)
		}
		catch(e){
			..logErr(">>>Timeline数据库创建表失败：",winform.txt时间线标题.text);
			..logErr(">>>Base数据库新增Timeline失败：",winform.txt时间线标题.text);
			..logErr(e) 
		}
		winform.txt时间线标题.text = "";
	}
	else {
	    //手动显示提示
    	var x,y,cx,cy = winform.txt时间线标题.getPos(true);
    	balloonTipCtrl.setText("请输入标题再创建").trackPopup(true,x+cx*3/4,y+cy,2000);
    	winform.balloonTipCtrlState = true;
	}
	updateCombobox(winform);
	
	win.delay(1000);
	winform.btn创建时间线.disabled = false;
}


import win.ui.simpleWindow;
win.ui.simpleWindow(winform,,,,).skin(
	background = { 
		hover = 0xff99ffcc;
		active = 0xffff6666; 
		default = 0x00000000; 
	}
	color = { 
		hover = 0xff666666;
		active = 0xffffffff; 
		default = 0xFF7BA5BF; 
	}
)

winform.btn删除时间线.oncommand = function(id,event){
	var confirmed = win.msgboxTest("确认删除时间线："""+winform.选择时间线.selText+"""吗？","确认删除");
	if(confirmed){
		
		var deleteTime = time(,"%Y年%m月%d日%H时%M分%S秒"); 
		var backupPath = fsys.createDir(dbDirBackUp + "/" + tostring(deleteTime) + "-删除【" + winform.选择时间线.selText + "】前备份",true);
		fsys.copy(..dbDir,backupPath)
		
		..dbTimeline.exec("DROP TABLE ??",{winform.选择时间线.selText})
		..logWarn(">>>已删除Base数据库Timeline中: ",winform.选择时间线.selText)
		..dbBase.exec("DELETE FROM ?? WHERE name=?;",{ "timeline",winform.选择时间线.selText} )
		..logWarn(">>>已删除Timeline数据库中的表：",winform.选择时间线.selText)
	}
	else {
		..logWarn(">>>取消删除")
	}

	updateCombobox(winform);
}

import win.ui.accelerator;
win.ui.accelerator({
	{ ctrl = true; vkey = 'G'#; oncommand = winform.btn创建时间线.oncommand } 
	{ ctrl = true; vkey = 'D'#; oncommand = winform.btn打开时间线.oncommand } 
},winform);

winform.wndproc = function(hwnd,message,wParam,lParam){
	select( message ) { 
		case 0x200/*_WM_MOUSEMOVE*/{
			if(winform.balloonTipCtrlState){
				balloonTipCtrl.popup(false);
				winform.balloonTipCtrlState = false;
			}
			
		}
		case 3/*_WM_MOVE*/{
			if(winform.balloonTipCtrlState){
    			var x,y,cx,cy = winform.txt时间线标题.getPos(true);
    			balloonTipCtrl.setText("请输入标题再创建").trackPopup(true,x+cx*3/4,y+cy,2000);
			}
		}
		else{
			
		}
	}
	//无返回值则继续调用默认回调函数
}

winform.show();
win.loopMessage();
return winform;