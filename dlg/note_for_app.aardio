import win.ui;
/*DSG{{*/
var appNotesWin = win.form(text="笔记";right=489;bottom=503;border="none")
appNotesWin.add(
exeNamePlus={cls="plus";left=0;top=0;right=488;bottom=40;dl=1;dr=1;dt=1;font=LOGFONT(h=-13;weight=700);z=1};
noteArea={cls="richedit";left=8;top=32;right=480;bottom=480;autohscroll=false;bgcolor=15790320;db=1;dl=1;dr=1;dt=1;font=LOGFONT(h=-13);link=1;multiline=1;readonly=1;vscroll=1;wrap=1;z=2};
保存={cls="button";text="保存";left=400;top=480;right=480;bottom=502;db=1;dr=1;z=3}
)
/*}}*/

import win.ui.simpleWindow;
win.ui.simpleWindow(appNotesWin,,,,).skin(
	background = { 
		hover = 0xff99ffcc;
		active = 0xffff6666; 
		default = 0x00000000; 
	}
	color = { 
		hover = 0xff666666;
		active = 0xffffffff; 
		default = 0xFF7BA5BF; 
	}
);

appNotesWin.exeName = "SHIFT";
appNotesWin.posChanged = false;

appNotesWin.updateNote = function(exeName){
	appNotesWin.exeName = exeName;
	appNotesWin.exeNamePlus.text = exeName;
	var result = ..dbAppNotes.stepQuery("SELECT * FROM [appNotesDefault]",{ name = appNotesWin.exeName})
	if(not result){
		var cmd =  ..dbAppNotes.prepare("REPLACE INTO [appNotesDefault] VALUES (@name,@content);" );
		cmd.step(  
			name = appNotesWin.exeName;
			content = "";
		);
		appNotesWin.noteArea.text = "";
	}
	else {
		appNotesWin.noteArea.text = result.content;
	}
	
	var result1 = ..dbAppNotes.stepQuery("SELECT * FROM [appNotesPos]",{ name = appNotesWin.exeName})
	if(result1){
		win.setPos(appNotesWin.hwnd,result1.xx,result1.yy,result1.xwidth,result1.yheight);
	}
	
}

appNotesWin.onClose = function(hwnd,message,wParam,lParam){
	appNotesWin.savePos();
	appNotesWin.show(false);
	return false; 
}

appNotesWin.saveData = function(){
	if(!appNotesWin.noteArea.readonly){
		appNotesWin.show(true);
		var ok = appNotesWin.msgboxTest("是否保存笔记？","保存笔记");
		if(ok){
			appNotesWin.保存.oncommand();
		}
	}
}

appNotesWin.savePos = function(){
	if(appNotesWin.posChanged){
		var xx1,yy1,xwidth1,yheight1 = win.getPos(appNotesWin.hwnd,true);
		var cmd1 = ..dbAppNotes.prepare("REPLACE INTO [appNotesPos] VALUES (@name,@xx,@yy,@xwidth,@yheight);" );
		cmd1.step(
			name = appNotesWin.exeName;
			xx = xx1;
			yy = yy1;
			xwidth = xwidth1;
			yheight = yheight1;
		);
		appNotesWin.posChanged = false;
	}
}


appNotesWin.forceClose = function(){
	appNotesWin.savePos();
	appNotesWin.saveData();
	appNotesWin.onClose = null;
	appNotesWin.close();
}

appNotesWin.noteArea.wndproc = function(hwnd,message,wParam,lParam){
	select(message) {
		case 0x201/*_WM_LBUTTONDOWN*/{
			if(appNotesWin.noteArea.readonly){
				appNotesWin.noteArea.readonly = false;
			}
		}
		case 8/*_WM_KILLFOCUS*/ {
			appNotesWin.savePos();
			appNotesWin.saveData();
		}
		else {
		}
	}
	//无返回值则继续调用默认回调函数
}

appNotesWin.保存.oncommand = function(id,event){
	appNotesWin.noteArea.readonly = true;
	
	var cmd =  ..dbAppNotes.prepare("REPLACE INTO [appNotesDefault] VALUES (@name,@content);" );
	cmd.step(
		name = appNotesWin.exeName;
		content = appNotesWin.noteArea.text;
	);
	appNotesWin.savePos();
}

appNotesWin.wndproc = function(hwnd,message,wParam,lParam){
	select( message ) { 
		case 0x47/*_WM_WINDOWPOSCHANGED*/{
			appNotesWin.posChanged = true;
		}
		else{
		}
	}
	//无返回值则继续调用默认回调函数
}

//appNotesWin.enableDpiScaling();
appNotesWin.show();
win.loopMessage();
return appNotesWin;