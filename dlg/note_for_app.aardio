import win.ui;
/*DSG{{*/
var appNotesWin = win.form(text="笔记";right=489;bottom=489;border="none")
appNotesWin.add(
exeNamePlus={cls="plus";left=0;top=0;right=488;bottom=40;dl=1;dr=1;dt=1;font=LOGFONT(h=-13;weight=700);z=1};
noteArea={cls="richedit";left=8;top=32;right=480;bottom=480;autohscroll=false;bgcolor=15790320;db=1;dl=1;dr=1;dt=1;font=LOGFONT(h=-13);link=1;multiline=1;readonly=1;vscroll=1;wrap=1;z=2}
)
/*}}*/

import win.ui.simpleWindow;
win.ui.simpleWindow(appNotesWin,,,,).skin(
	background = { 
		hover = 0xff99ffcc;
		active = 0xffff6666; 
		default = 0x00000000; 
	}
	color = { 
		hover = 0xff666666;
		active = 0xffffffff; 
		default = 0xFF7BA5BF; 
	}
);

appNotesWin.exeName = "SHIFT";

appNotesWin.updateNote = function(exeName){
	appNotesWin.exeName = exeName;
	appNotesWin.exeNamePlus.text = exeName;
	var result = ..dbAppNotes.stepQuery("SELECT * FROM [appNotesDefault]",{ name = appNotesWin.exeName})
	if(not result){
		var cmd =  ..dbAppNotes.prepare("REPLACE INTO [appNotesDefault] VALUES (@name,@content);" );
		cmd.step(  
			name = appNotesWin.exeName;
			content = "";
		);
		appNotesWin.noteArea.text = "";
	}
	else {
		appNotesWin.noteArea.text = result.content;
	}
}

appNotesWin.onClose = function(hwnd,message,wParam,lParam){
	appNotesWin.show(false);
	appNotesWin.saveDate();
	return false; 
}

appNotesWin.saveDate = function(){
	if(!appNotesWin.noteArea.readonly){
		var cmd =  ..dbAppNotes.prepare("REPLACE INTO [appNotesDefault] VALUES (@name,@content);" );
		cmd.step(
			name = appNotesWin.exeName;
			content = appNotesWin.noteArea.text;
		);
	};
}

appNotesWin.forceClose = function(){
	appNotesWin.onClose = null;
	appNotesWin.saveDate();
	appNotesWin.close();
}

appNotesWin.noteArea.wndproc = function(hwnd,message,wParam,lParam){
	select(message) {
		case 0x201/*_WM_LBUTTONDOWN*/{
			if(appNotesWin.noteArea.readonly){
				appNotesWin.noteArea.readonly = false;
				//appNotesWin.noteArea.foreground = 0xFFFFFFFF;
			}
		}
		case 8/*_WM_KILLFOCUS*/ {
			appNotesWin.saveDate();
		}
		else {
		}
	}
	//无返回值则继续调用默认回调函数
}



appNotesWin.show();
win.loopMessage();
return appNotesWin;